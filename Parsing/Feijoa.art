(*******************************************************************************
*
* feijoa (by antonio oladuti - 2022/23)
* computer science dept. @ royal holloway, university of london
*
*******************************************************************************)
prelude {import java.util.HashMap; import java.util.ArrayList; import java.util.Scanner;}

support {
	
	FJCallStack callStack = new FJCallStack();
	STable symbols = new STable(callStack);
	FJSDStack SDStack = symbols.SDStack();
	FJSAStack SAStack = new FJSAStack(symbols);
	FJIDStack ids = new FJIDStack();

	@SuppressWarnings("serial")
	public class FJSigReturn extends ARTException {
	private Value value;

	public FJSigReturn(String message) {
		super(message);
	}

	public FJSigReturn() {
		this("");
	}
	}
}

whitespace &SIMPLE_WHITESPACE
whitespace &COMMENT_LINE_C
whitespace &COMMENT_BLOCK_C

statements ::= statement | statement statements
compStatements ::= '{' '}' | scopeEnter statements scopeLeave | { symbols.newScope(); } statement { symbols.oldScope(); }
statement ::= defStruct | defFunc | loopStatement | condStatement | mainExpr ';' | returnExpr ';'
structDefinition ::= '{' structStatements '}'
structStatements ::= structStatement | structStatement structStatements
structStatement ::= mainExpr ';' | defStruct

fnStatements ::= '{' statements '}'
lambdaStatements ::= mainExpr | returnExpr | fnStatements

scopeEnter ::= '{' { symbols.newScope(); }
scopeLeave ::= '}' { symbols.oldScope(); }

defFunc ::= 'def' ID { ids.push(new FJFunction(ID1.v)); } mainDefFuncArgs fnStatements< 
	{	FJFunction fn = ids.top();
		fn.setProcedure(defFunc.fnStatements1);
		symbols.put(fn.name, fn);
		ids.pop(); }

lambdaConstruct <o: Object> ::= { ids.push(new FJFunction("")); } mainDefFuncArgs '->' lambdaStatements< 
	{ 
		ids.top().setProcedure(lambdaConstruct.lambdaStatements1);
		lambdaConstruct.o = ids.pop(); }

mainDefFuncArgs ::= '(' ')' | '(' defFuncArgs ')' | defFuncArg

defFuncArgs <c:int> ::= defFuncArg ',' defFuncArgs
	| defFuncArg

defFuncArg <o: Object> ::= argID optEquals
	{ ids.top().addArg(argID1.v, optEquals1.o); }

optEquals <o: Object> ::= # { optEquals.o =  null; }
	| '=' subExprA { optEquals.o = subExprA1.o; }

argID <v: String> ::= ID ':' AID {argID.v = ID1.v;} | ID { argID.v = ID1.v; }
defStruct ::= 'struct' ID 
		{ symbols.startStructDef(ID1.v); } structDefinition { symbols.endStructDef(); }

loopStatement ::= whileExpr< compStatements<
		{	artEvaluate(
					loopStatement.whileExpr1, whileExpr1);
			while (FJ.bv(whileExpr1.o) == true) { 
				artEvaluate(
					loopStatement.compStatements1, compStatements1); 
                artEvaluate(
					loopStatement.whileExpr1, whileExpr1); 
            }
		}
	| untilExpr< compStatements<
		{	artEvaluate(
					loopStatement.untilExpr1, untilExpr1);
			while (FJ.bv(untilExpr1.o) == false) { 
				artEvaluate(
					loopStatement.compStatements1, compStatements1); 
                artEvaluate(
					loopStatement.untilExpr1, untilExpr1); 
            }
		}

condStatement ::= ifExpr compStatements<
		{	if (FJ.bv(ifExpr1.o) == true) {
				artEvaluate(condStatement.compStatements1, compStatements1); 
			}
		}
	| ifExpr compStatements< elifExpr compStatements<
		{ if (FJ.bv(ifExpr1.o) == true) {
			artEvaluate(condStatement.compStatements1, compStatements1); 
			}
		  else if (FJ.bv(elifExpr1.o) == true){
			artEvaluate(condStatement.compStatements2, compStatements2);  
		  }
		}
	| elifExpr compStatements< elifExpr compStatements<
		{	
			if (FJ.bv(elifExpr1.o) == false && FJ.bv(elifExpr2.o) == true) {
				artEvaluate(condStatement.compStatements2, compStatements2); 
			}
		}
	| ifExpr compStatements< 'else' compStatements<
		{
			if (FJ.bv(ifExpr1.o) == false) {
				artEvaluate(condStatement.compStatements2, compStatements2);
			} else {
				artEvaluate(condStatement.compStatements1, compStatements1);
			}
		}
	| elifExpr compStatements< 'else' compStatements<
		{
			if (FJ.bv(elifExpr1.o) == false) {
				artEvaluate(condStatement.compStatements2, compStatements2);
			}
		}
		
		
whileExpr <o: Object> ::= 'while' subExprA
	{ whileExpr.o = subExprA1.o; }

untilExpr <o: Object> ::= 'until' subExprA
	{ untilExpr.o = subExprA1.o; }
	
ifExpr <o: Object> ::= 'if' subExprA
	{ ifExpr.o = subExprA1.o; }
	
elifExpr <o: Object> ::= 'elif' subExprA
	{ elifExpr.o = subExprA1.o; }
	
sysout ::= 'sysout' '(' subExprA ')'
	{ System.out.print(subExprA1.o); }
	
sysline ::= 'sysline' '(' subExprA ')'
	{ System.out.println(subExprA1.o); }

sysin <o: Object> ::= 'sysin' '(' ')' 
	{ sysin.o = new Scanner(System.in).nextLine(); }
	
returnExpr <o: Object> ::= 'return' subExprA 
	{  callStack.setLastValue(subExprA1.o);
		if (true) throw new FJSigReturn(); }

memberAssign ::= AID '=' subExprA
		{ SDStack.addMember(AID1.v, subExprA1.o); }
	| defStruct
	
mainExpr <o: Object> ::= assign 
	| subExprA { mainExpr.o = subExprA1.o; }

mainFuncArgs ::= '(' ')' { callStack.fillArgs(); } | '(' funcArgs ')'

functionCall <o: Object> ::= voidFunctions { functionCall.o = null; }
	| otherFunctions { functionCall.o = otherFunctions1.o; }
	| AID
		{
		  callStack.push((FJFunction) symbols.get(AID1.v));
		} mainFuncArgs
	{	
		try {
			callStack.nextCall();
			symbols.newScope();
			artEvaluate(callStack.topProcedure(), null);
		} catch (FJSigReturn FJsig) {
			;
		} finally {
			functionCall.o = callStack.getLastValue();
			callStack.pop();
			symbols.oldScope();
		}
	}

funcArgs <o: Object> ::= subExprA argNote { callStack.processArg(subExprA1.o); } ',' funcArgs
	| ID argNote '=' subExprA { callStack.processArg(ID1.v, subExprA1.o); } ',' funcArgs
	| subExprA argNote { callStack.processLastArg(subExprA1.o); }
	| ID argNote '=' subExprA { callStack.processLastArg(ID1.v, subExprA1.o); }

voidFunctions ::= sysout | sysline
otherFunctions <o:Object> ::= sysin { otherFunctions.o = sysin1.o; }

subExprA <o: Object> ::= subExprB { subExprA.o = subExprB1.o; }
	| and { subExprA.o = and1.o; }
	| or  { subExprA.o = or1.o;  }
	| gtr { subExprA.o = gtr1.o; }
	| lss { subExprA.o = lss1.o; }
	| equ { subExprA.o = equ1.o; }
	
subExprB <o: Object> ::= subExprC { subExprB.o = subExprC1.o; }
	| plus { subExprB.o = plus1.o; }
	| minus { subExprB.o = minus1.o; }
	
subExprC <o: Object> ::= subExprD { subExprC.o = subExprD1.o; }
	| multiply { subExprC.o = multiply1.o; }
	| divide { subExprC.o = divide1.o; }

subExprD <o: Object> ::= subExprE { subExprD.o = subExprE1.o; }
	| exponentiate { subExprD.o = exponentiate1.o; }
	
subExprE <o: Object> ::= subExprF { subExprE.o = subExprF1.o; }
	| not { subExprE.o = not1.o; }
	| negate { subExprE.o = negate1.o; }
	
subExprF <o: Object> ::= operand { subExprF.o = operand1.o; }

equ <o: Object> ::= subExprB '==' subExprB
	{ equ.o = FJ.equ(subExprB1.o, subExprB2.o); }

gtr <o: Object> ::= subExprB '>' subExprB
	{ gtr.o = FJ.gtr(subExprB1.o, subExprB2.o); }
	
lss <o: Object> ::= subExprB '<' subExprB
	{ lss.o = FJ.lss(subExprB1.o, subExprB2.o); }
		
assign ::= AID '=' subExprA
		{ symbols.put(AID1.v, subExprA1.o); }
	| plusEquals
	| minusEquals
	
structConstruct <o: Object> ::= 'new' AID 
	{	SAStack.push(AID1.v); } mainStructArgs { structConstruct.o = SAStack.popStruct(); }


mainStructArgs ::= '(' ')' { SAStack.fillArgs(); } | '(' structArgs ')'

structArgs <o: Object> ::= subExprA argNote { SAStack.processArg(subExprA1.o); } ',' structArgs
	| ID argNote '=' subExprA { SAStack.processArg(ID1.v, subExprA1.o); } ',' structArgs
	| subExprA argNote { SAStack.processLastArg(subExprA1.o); }
	| ID argNote '=' subExprA { SAStack.processLastArg(ID1.v, subExprA1.o); }

argNote ::= ':' AID | #
	
plusEquals <o: Object> ::= AID '+=' subExprC	
	{ symbols.put(AID1.v, FJ.plus(symbols.get(AID1.v), subExprC1.o)); }
	
minusEquals <o: Object> ::= AID '-=' subExprC
	{ symbols.put(AID1.v, FJ.minus(symbols.get(AID1.v), subExprC1.o)); }
		
plus <o: Object> ::= subExprB '+' subExprC
	{ plus.o = FJ.plus(subExprB1.o, subExprC1.o); }	
					
minus <o: Object> ::= subExprB '-' subExprC
	{ minus.o = FJ.minus(subExprB1.o, subExprC1.o); }
	
multiply <o: Object> ::= subExprC '*' subExprD
	{ multiply.o = FJ.multiply(subExprC1.o, subExprD1.o); }
	
divide <o: Object> ::= subExprC '/' subExprD
	{ divide.o = FJ.divide(subExprC1.o, subExprD1.o); }
		
exponentiate <o: Object> ::= subExprE '**' subExprD
	{ exponentiate.o = FJ.exponentiate(subExprE1.o, subExprD1.o); }
		
not <o: Object> ::= '!' subExprE
	{ not.o = !(FJ.bv(subExprE1.o)); }
	
negate <o: Object> ::= '-' subExprE
	{	try {
			negate.o = FJ.iv(subExprE1.o) * -1; 
		} catch (Exception FJException) {
			negate.o = FJ.dv(subExprE1.o) * -1;
		}
	}
	
and <o: Object> ::= subExprA '&&' subExprA 
	{ and.o = (FJ.bv(subExprA1.o)
			&& FJ.bv(subExprA2.o)); }

or <o: Object> ::= subExprA '||' subExprA 
	{ or.o = FJ.bv(subExprA1.o) || FJ.bv(subExprA2.o); }
	
operand <o: Object> ::= deref { operand.o = symbols.get(deref1.v); }

	| INTEGER
		{ operand.o = INTEGER1.v; }
	| REAL
		{ operand.o = REAL1.v; }
	| 'true'
		{ operand.o = FJ.bv(true); }
	| 'false'
		{ operand.o = FJ.bv(false); }
	| STRING_DQ
		{ operand.o = STRING_DQ1.v; }
	| lambdaConstruct
		{ operand.o = lambdaConstruct1.o; }
	| structConstruct
		{ operand.o = structConstruct1.o; }
	| structConstruct '.' AID 
		{ @SuppressWarnings("unchecked")
		 HashMap<String, Object> hm = (HashMap<String, Object>) structConstruct1.o;
		 operand.o = symbols.fullGet(AID1.v.split(STable.selectOp), hm); }
	| functionCall { operand.o = functionCall1.o; }
	| '(' mainExpr ')'
		{ operand.o = mainExpr1.o; }
	| #
		
deref <v: String> ::= AID
	{ deref.v = AID1.v; }

AID <v:String> ::= ID { AID.v = ID1.v; } | ID '.' AID { AID.v = ID1.v + '.' + AID1.v; }

ID <leftExtent:int rightExtent:int lexeme:String v:String> ::= 
  &ID {ID.lexeme = artLexeme(ID.leftExtent, ID.rightExtent); ID.v = artLexemeAsID(ID.leftExtent, ID.rightExtent); }  

INTEGER <leftExtent:int rightExtent:int lexeme:String v:int> ::= 
  &INTEGER {INTEGER.lexeme = artLexeme(INTEGER.leftExtent, INTEGER.rightExtent); INTEGER.v = artLexemeAsInteger(INTEGER.leftExtent, INTEGER.rightExtent); }  

REAL <leftExtent:int rightExtent:int lexeme:String v:Double> ::= 
  &REAL {REAL.lexeme = artLexeme(REAL.leftExtent, REAL.rightExtent); REAL.v = artLexemeAsReal(REAL.leftExtent, REAL.rightExtent); }
  
STRING_DQ <leftExtent:int rightExtent:int lexeme:String v:String> ::= 
  &STRING_DQ {STRING_DQ.lexeme = artLexeme(STRING_DQ.leftExtent, STRING_DQ.rightExtent); STRING_DQ.v = artLexemeAsString(STRING_DQ.leftExtent, STRING_DQ.rightExtent); }